
function(COMPILE_SHADER shader_file output_dir)
    # 获取文件的扩展名（例如，.vert 或 .frag）
    get_filename_component(SHADER_EXT ${shader_file} EXT)
    get_filename_component(SHADER_NAME ${shader_file} NAME_WE)
    string(SUBSTRING ${SHADER_EXT} 1 -1 SHADER_EXT)       # 去掉最前面的 “.” 符号
    string(TOUPPER ${SHADER_EXT} SHADER_EXT)
    string(TOUPPER ${SHADER_NAME} SHADER_NAME)
    set(OUTPUT_NAME "${SHADER_NAME}_${SHADER_EXT}")

    # 提取最近一级的目录名
    get_filename_component(DIR_NAME ${shader_file} DIRECTORY)
    get_filename_component(PARENT_DIR "${DIR_NAME}" NAME)

    # 构建输出文件路径
    set(output_file "${output_dir}/${PARENT_DIR}/${OUTPUT_NAME}.spv")

    # 检查输出目录是否存在，如果不存在则创建
    if(NOT EXISTS ${output_dir}/${PARENT_DIR})
        file(MAKE_DIRECTORY ${output_dir}/${PARENT_DIR})
    endif()

    # 调用 glslangValidator 编译 GLSL 文件
    execute_process(
            COMMAND ${GLSL_LANG_VALIDATOR} -V ${shader_file} -o ${output_file}
            RESULT_VARIABLE result
            OUTPUT_VARIABLE output
            ERROR_VARIABLE error
    )

    # 检查命令是否成功
    if(NOT result EQUAL 0)
        message(FATAL_ERROR "Failed to compile shader: ${shader_file}\nError: ${output}")
    else()
        message(STATUS "Shader compiled successfully: ${shader_file} -> ${output_file}")
    endif()
endfunction()

# 文件夹不存在则创建，存在则删除并创建
function(MAKE_DIR_FORCE PATH)
    if(NOT EXISTS ${output_dir})
        file(MAKE_DIRECTORY ${output_dir})
    else ()
        file(REMOVE_RECURSE ${output_dir})
        file(MAKE_DIRECTORY ${output_dir})
    endif()
endfunction()

# 遍历 shader 目录及其子目录，编译所有 GLSL 文件
function(COMPILE_ALL_SHADER input_dir output_dir)
    MAKE_DIR_FORCE(${output_dir})

    # 获取所有的 .vert 和 .frag 文件（包括子目录）
    file(GLOB_RECURSE shaders "${input_dir}/*.vert" "${input_dir}/*.frag")
    if(NOT shaders)
        message(WARNING "Unable to find any shader file, automatically quit compilation.")
        return()
    endif ()

    # 遍历所有 GLSL 文件，调用 compile_shader 函数进行编译
    foreach(shader IN LISTS shaders)
        compile_shader(${shader} ${output_dir})
    endforeach()
endfunction()


function(TRANSFORM_SPV SPV_FILE OUTPUT_DIR)
    get_filename_component(OUTPUT_NAME ${SPV_FILE} NAME_WE)

    # 提取最近一级的目录名
    get_filename_component(DIR_NAME ${SPV_FILE} DIRECTORY)
    get_filename_component(PARENT_DIR "${DIR_NAME}" NAME)

    # 构建输出文件路径
    set(OUTPUT_FILE_NAME "${OUTPUT_DIR}/${PARENT_DIR}/${OUTPUT_NAME}.h")

    string(TIMESTAMP CURRENT_TIME "%Y-%m-%d %H:%M:%S")
    set(OUTPUT_FILE_HEADER "// This file is automatically generated, please do not alter it.\n// ${CURRENT_TIME}\n")
    set(OUTPUT_FILE_DECLARE "#include <vector>\n\nstd::vector<uint8_t> ${OUTPUT_NAME} = {\n")

    file(READ ${SPV_FILE} HEX_CONTENT HEX)
    string(REPEAT "[0-9a-f]" 32 pattern)
    string(REGEX REPLACE "(${pattern})" "\t\\1\n" CONTENT "${HEX_CONTENT}")
    string(REGEX REPLACE "([0-9a-f][0-9a-f])" "0x\\1, " CONTENT "${CONTENT}")
    string(REGEX REPLACE ", $" "" CONTENT "${CONTENT}")

    file(WRITE ${OUTPUT_FILE_NAME} "${OUTPUT_FILE_HEADER}\n${OUTPUT_FILE_DECLARE} ${CONTENT}\n};")

    message(STATUS "Shader transformed successfully: ${SPV_FILE} -> ${OUTPUT_FILE_NAME}")
endfunction()

# 将所有的spv二进制文件保存在头文件里
function(TRANSFORM_ALL_SPV INPUT_DIR OUTPUT_DIR)
    MAKE_DIR_FORCE(${OUTPUT_DIR})

    file(GLOB_RECURSE files "${INPUT_DIR}/*.spv")

    foreach(file IN LISTS files)
        TRANSFORM_SPV(${file} ${OUTPUT_DIR})
    endforeach()
endfunction()
